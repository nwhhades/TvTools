plugins {
    id 'com.android.library'
    id 'maven-publish'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

def groupIdStr = "com.nwhhades"
def artifactIdStr = "common"
def versionStr = "1.0"

android {
    compileSdk 32

    defaultConfig {
        minSdk 19
        targetSdk 32

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    android.libraryVariants.all { variant ->
        // aar 输出文件名配置
        variant.outputs.all { output ->
            outputFileName = "${artifactIdStr}-${versionStr}.aar"
        }
    }

    publishing {
        singleVariant("release")
    }

    //配置这个并不是让你隐藏过时api，而是让过时api或者不合规矩的语法展现出来
    allprojects {
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            }
        }
    }

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                //组包名
                groupId = groupIdStr
                //依赖库名称
                artifactId = artifactIdStr
                //依赖库版本号
                version = versionStr
            }
        }
    }
}

dependencies {

    api 'androidx.appcompat:appcompat:1.5.1'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    //工具箱-不需要混淆规则
    api 'com.blankj:utilcodex:1.31.1'
    //图片库-不需要混淆规则
    api 'com.github.bumptech.glide:glide:4.14.2'
    //吐司框架-不需要混淆 https://github.com/getActivity/Toaster
    api 'com.github.getActivity:Toaster:12.0'
    //权限请求框架-不需要混淆 https://github.com/getActivity/XXPermissions
    api 'com.github.getActivity:XXPermissions:16.8'
    //http框架-需要混淆
    api 'com.github.getActivity:EasyHttp:11.6'
    //okhttp-需要混淆
    //noinspection GradleDependency
    api 'com.squareup.okhttp3:okhttp:3.12.13'
    //缓存-需要混淆
    api 'com.tencent:mmkv:1.2.15'

}